apply plugin: 'com.android.library'

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: "com.jfrog.bintray"

def libVersionCode = 3
def libVersionName = "0.3"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 25
        versionCode libVersionCode
        versionName libVersionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.2.0'
    testCompile 'junit:junit:4.12'
}

// Publishing to jcenter()

def MAVEN_GROUP_ID = "ro.mdc_software"
def MAVEN_ARTIFACT_ID = "basenumber"
def MAVEN_VERSION = libVersionName
def MAVEN_PROJECT_NAME = "BaseNumberConverterLibrary"

def SITE_URL = 'http://www.mdc-software.ro'
def GIT_URL = 'https://github.com/CosminMihuMDC/BaseNumberConverter.git'
def ISSUE_TRACKER_URL = 'https://github.com/CosminMihuMDC/BaseNumberConverter/issues'

def SCM_CONNECTION = "scm:git:git://github.com/CosminMihuMDC/BaseNumberConverter.git"
def SCM_DEV_CONNECTION = "scm:git:ssh://git@github.com/CosminMihuMDC/BaseNumberConverter.git"

group MAVEN_GROUP_ID
version MAVEN_VERSION

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                name MAVEN_PROJECT_NAME
                description = 'This library can convert a number between two custom bases (between 2 and 62).'
                url SITE_URL

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'CosminMihuMDC'
                        name 'Cosmin Mihu'
                        email 'cosmin.mihu@gmail.com'
                    }
                }
                scm {
                    connection SCM_CONNECTION
                    developerConnection SCM_DEV_CONNECTION
                    url SITE_URL
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
//    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = 'android'
        name = MAVEN_PROJECT_NAME // It is the name that appears in bintray when logged
        desc = "This library can convert a number between two custom bases (between 2 and 62)."
        websiteUrl = SITE_URL
        vcsUrl = GIT_URL
        issueTrackerUrl = ISSUE_TRACKER_URL
        labels = ['android', 'math', 'math-base', 'number', 'binary', 'hex', 'decimal', 'hexadecimal', 'converter']
        licenses = ["Apache-2.0"]
        publish = true

        version {
            name = MAVEN_VERSION
            released = new Date()
            vcsTag = 'release-v.' + MAVEN_VERSION
        }
    }
}
